plugins {
	id 'java'
	id "dev.architectury.loom" version "1.6-SNAPSHOT" apply(false)
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

architectury {
	minecraft = minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"
	apply plugin: "architectury-plugin"
	apply plugin: "com.github.johnrengelman.shadow"

	loom {
		silentMojangMappingsLicense()
	}

	repositories {
		maven {
			name = "Jared's maven"
			url = "https://maven.blamejared.com/"
		}
		maven {
			name = "TerraformersMC"
			url = "https://maven.terraformersmc.com"
		}
		maven {
			url "https://cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
		maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }
		maven { url = "https://maven.neoforged.net/releases/" }
		maven { url = "https://maven.minecraftforge.net/" }
		maven { url = "https://maven.architectury.dev//" }
	}

	dependencies {
		minecraft "com.mojang:minecraft:${minecraft_version}"
		mappings loom.layered() {
			officialMojangMappings()
			parchment("org.parchmentmc.data:parchment-${minecraft_version}:$parchment_version@zip")
		}
		modApi "com.teamresourceful.resourcefulconfig:resourcefulconfig-$name-$minecraft_version:$resourcefulconfig_version"

	}

	tasks.withType(JavaCompile).tap {
		configureEach {
			options.encoding = "UTF-8"
			options.release = 17
		}
	}

	java {
		withSourcesJar()
		toolchain.languageVersion = JavaLanguageVersion.of(17)
	}

	jar {
		archiveClassifier.set "dev"
		archivesBaseName = "$mod_id-$name-$minecraft_version"
	}

	processResources {
		def expandProps = [
				"version": version,
				"group": project.group, //Else we target the task's group.
				"minecraft_version": minecraft_version,
				"forge_version": forge_version,
				"neoforge_version": neoforge_version,
				"fabric_version": fabric_version,
				"fabric_loader_version": fabric_loader_version,
				"mod_id": mod_id,
				"resourcefulconfig_version": resourcefulconfig_version
		]

		filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
			expand expandProps
		}
		inputs.properties(expandProps)
	}

}

