// ForgeGradle Buildscript
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

// Plugins
plugins {
	id 'com.rlonryan.quikmod' version '1.0.16'
    id 'com.diffplug.gradle.spotless' version '2.3.0'
}

// Tweak MineCraft Task
minecraft {
    makeObfSourceJar = false
	useDepAts = true
}

repositories {
    maven {
        name "chickenbones"
        url  "http://chickenbones.net/maven/"
    }
    maven {
        name "Mobius Repo"
        url  "http://mobiusstrip.eu/maven/"
    }
    maven {
        name "sponge"
        url  "http://repo.spongepowered.org/maven/"
    }
	maven {
        name "sk89q"
        url  "http://maven.sk89q.com/repo/"
    }
	maven {
		name "JEI"
		url "http://dvs1.progwml6.com/files/maven"
	}
	 maven { 
		name "OpenComputers"
		url  "http://maven.cil.li/"
	}
}

dependencies {
    // Mods (For auto-import)
	compile project(':lib/InfinityLib')
	shade project(':lib/AgriCore')
	compile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4:dev"
	deobfCompile "mezz.jei:jei_1.10.2:3.7.6.231"
}

processResources {
	from('lib/AgriPlants') {
		include '**/*.json'
		into 'json/defaults'
	}
}

// Javadoc
task mainJavadoc(type: Javadoc) {
	source = sourceSets.main.allJava
	title = "AgriCraft ${mod.version} Documentation"
	destinationDir = file("${project.docsDir}/mainJavadoc")
	failOnError = false
}

task apiJavadoc(type: Javadoc) {
	source = sourceSets.main.allJava
	include "**/api/**"
	title = "AgriCraft ${mod.version} API Documentation"
	destinationDir = file("${project.docsDir}/apiJavadoc")
	failOnError = false
}

// Jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: mainJavadoc) {
    classifier = 'javadoc'
    from "${project.docsDir}/mainJavadoc"
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include "${mod.package.replace('.', '/')}/api/**/*"
    classifier = 'api'
}

// Tell build that it needs the API compiled first.
build.dependsOn apiJar

// Code Formatting
spotless {
    java {
        // Ensure ~only~ AgriCraft code is formatted.
        target 'src/main/java/com/infinityraider/agricraft/**/*.java'
        
        // Import Ordering
        importOrder([
            'java',
            'javax',
            'org',
            'com',
            'net',
            'com.infinityraider',
            'com.agricraft',
            ''
        ])
        
        // Initiate Flamewar
        indentWithSpaces(4)
        
        // Sanitize Whitespace
        trimTrailingWhitespace()
        endWithNewline()
    }
}
